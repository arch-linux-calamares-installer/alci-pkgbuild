# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# https://gitlab.manjaro.org/manjaro-arm/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares-git/-/blob/master/PKGBUILD

pkgname=alci-calamares-dev
_pkgname=calamares
pkgver=3.3.13.241220
#_pkgver=3.3.12
_replacethisversion=3.3.12
pkgrel=01
_commit=ed92ad6
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares/"
license=('LGPL')
depends=(
	'boost-libs'
	'ckbcomp'
	'cryptsetup'
	'doxygen'
	'efibootmgr'
	'gptfdisk'
	'gtk-update-icon-cache'
	'hwinfo'
	'icu'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kparts'
	'kpmcore'
	'kservice'
	'kwidgetsaddons'
	'libpwquality'
	'mkinitcpio-openswap'
	'polkit-qt6'
	'rsync'
	'qt6-declarative'
	'solid'
	'squashfs-tools'
	'yaml-cpp'
)
makedepends=(
	'boost'
	'cmake'
	'extra-cmake-modules'
	'git'
	'ninja'
	'python-jsonschema'
	'python-pyaml'
	'python-unidecode'
	'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=($_pkgname::git+https://github.com/calamares/calamares#commit=$_commit
	"calamares.desktop"
	"calamares_polkit"
	"49-nopasswd-calamares.rules")

sha256sums=('3b4fe2d0145ad2ff1e3a0e9c295afcbfecf555cbf2b56d5b6513f8a4b9ffa0e1'
            'd6a5ce002c99586dfa0cb8621cad631f148dfd0d6d234c31643a63c585c546ee'
            'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176')

prepare() {
	cd ${srcdir}/calamares
	sed -i -e 's/etc\/sddm.conf/etc\/sddm.conf.d\/kde_settings.conf/' src/modules/displaymanager/main.py

	# change version
	_ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
	sed -i -e "s|CALAMARES_VERSION $_replacethisversion|CALAMARES_VERSION $pkgver|g" CMakeLists.txt

}

build() {
	cd $_pkgname

  cmake -S . -Bbuild \
      -GNinja \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DWITH_APPSTREAM=OFF \
      -DWITH_PYBIND11=OFF \
      -DWITH_QT6=ON \
      -DSKIP_MODULES="dracut \
          dracutlukscfg \
          dummycpp \
          dummyprocess \
          dummypython \
          dummypythonqt \
          initramfs \
          initramfscfg \
          interactiveterminal \
          keyboardq \
          license \
          localeq \
          oemid \
          packagechooserq \
          partitionq \
          services-openrc \
          summaryq \
          tracking \
          usersq \
          welcomeq \
          zfs \
          zfshostid"

  cmake --build build
}
package() {
	cd ${srcdir}/calamares/build
	DESTDIR="${pkgdir}" cmake --build . --target install
	install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
	install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750 "$pkgdir"/etc/polkit-1/rules.d
}
